plugins {
	id "com.gradle.plugin-publish" version "0.9.1"
	id 'net.saliman.cobertura' version '2.3.1'
}


cobertura.setCoverageFormats(['html', 'xml'].toSet())

apply plugin: 'maven'
apply plugin: 'signing'

repositories {
	mavenCentral()
}

task wrapper(type: Wrapper) {
	gradleVersion = '3.1'
}

signing {
	sign configurations.archives
}

dependencies {
	compile gradleApi()
	compile 'org.apache.httpcomponents:httpclient:4.4.1'
	compile 'org.apache.httpcomponents:httpmime:4.4.1'
	compile 'org.pegdown:pegdown:1.6.0'
	compile 'org.openbakery.coverage:CoverageReport:0.9.3'
}

jar {
	baseName 'xcode-plugin'
	from project.sourceSets.main.output
	from project(":libxcode").sourceSets.main.output
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	from project(":libxcode").sourceSets.main.allSource
	classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = 'javadoc'
	from tasks.javadoc.destinationDir
}

artifacts {
	archives jar

	archives javadocJar
	archives sourcesJar
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: publishUser, password: publishPassword)
			}
			
			pom.project {
				name 'XCode-Plugin'
				packaging 'jar'
				description 'XCode-Plugin is a plugin to allow custom XCode projects to build as generated by CMake'
				url 'https://github.com/gpx1000/gradle-xcodePlugin'

				licenses {
					license {
						name 'The Apache Software License, Version 2.0'
						url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
						distribution 'repo'
					}
				}
				scm {
					url 'scm:git@github.com:gpx1000/xcode-plugin.git'
					connection 'scm:git@github.com:gpx1000/xcode-plugin.git'
					developerConnection 'scm:git@github.com:gpx1000/xcode-plugin.git'
				}
				developers {
					developer {
						id "renep"
						name "Ren√© Pirringer"
						email "rene@openbakery.org"
					}
					developer {
						id "gpx1000"
						name "Steven Winston"
						email "gpx1000@gmail.com"
					}
				}
			}
		}
	}
}




pluginBundle {
	website = 'http://gpxblog.com'
	vcsUrl = 'https://github.com/gpx1000/gradle-xcodePlugin'
	description = 'A gradle plugin for building Xcode projects! Generated by CMakeLists'
	tags = ['xcode', 'iOS', 'os x', "osx", "mac", "iphone", "ipad", 'android', 'studio']

	plugins {
		xcodePlugin {
			id = 'com.gpxblog.xcode-plugin'
			displayName = 'Gradle Xcode plugin from cmake'
		}
	}
}
